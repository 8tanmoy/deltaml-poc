* MALONALDEHYDE by tanmoy
* Toppar stream file generated by
* CHARMM General Force Field (CGenFF) program version 2.4.0
* For use with CGenFF version 4.4
*

!--load topology for small molecules--
set t /projectnb/cui-buchem/tanmoy/projects/toppar

read rtf card name @t/top_all36_cgenff.rtf
read para card flex name @t/par_all36_cgenff.prm
stream @t/toppar_water_ions.str

read rtf card append
* Topologies generated by
* CHARMM General Force Field (CGenFF) program version 2.4.0
* using valence-based bond orders
*
36 1

! "penalty" is the highest penalty score of the associated parameters.
! Penalties lower than 10 indicate the analogy is fair; penalties between 10
! and 50 mean some basic validation is recommended; penalties higher than
! 50 indicate poor analogy and mandate extensive validation/optimization.

RESI MA       0.000 ! param penalty=  47.000 ; charge penalty=  33.130
GROUP            ! CHARGE   CH_PENALTY
ATOM C1     CG2O4   0.199 !   28.459
ATOM C2     CG321   0.034 !   33.130
ATOM C3     CG2O4   0.199 !   28.459
ATOM O1     OG2D1  -0.398 !   15.973
ATOM O2     OG2D1  -0.398 !   15.973
ATOM H1     HGR52   0.092 !    2.450
ATOM H2     HGA2    0.090 !    0.000
ATOM H3     HGA2    0.090 !    0.000
ATOM H4     HGR52   0.092 !    2.450
               ! Bond order
BOND H1   C1   ! 1
BOND H2   C2   ! 1
BOND C1   C2   ! 1
BOND C1   O2   ! 2
BOND O1   C3   ! 2
BOND C2   C3   ! 1
!BOND C2   H3   ! 1
BOND C3   H4   ! 1
IMPR C1     C2     O2     H1    
IMPR C3     C2     O1     H4    

END

read param card flex append
* Parameters generated by analogy by
* CHARMM General Force Field (CGenFF) program version 2.4.0
*

! Penalties lower than 10 indicate the analogy is fair; penalties between 10
! and 50 mean some basic validation is recommended; penalties higher than
! 50 indicate poor analogy and mandate extensive validation/optimization.

BONDS
CG2O4  CG321   250.00     1.5000 ! PALD, propionaldehyde from AALD adm 11/08
CG2O4  OG2D1   700.00     1.2150 ! ALDEHYDE acetaldehyde adm 11/08
CG2O4  HGR52   330.00     1.1100 ! ALDEHYDE acetaldehyde adm 11/08
CG321  HGA2    309.00     1.1110 ! PROT alkane update, adm jr., 3/2/92

ANGLES
CG321  CG2O4  OG2D1    45.00    126.00 ! ALDEHYDE propionaldehyde adm 11/08
CG321  CG2O4  HGR52    65.00    116.00 ! ALDEHYDE propionaldehyde adm 11/08
OG2D1  CG2O4  HGR52    65.00    118.00 ! ALDEHYDE acetaldehyde adm 11/08
CG2O4  CG321  CG2O4    51.80    107.50 ! Properti , from CG2O2 CG321 CG2R61, PENALTY= 27
CG2O4  CG321  HGA2     33.00    109.50   30.00   2.16300 ! PALD, propionaldehyde from PROT adm jr. 5/02/91, Consistent with adm 11/08
HGA2   CG321  HGA2     35.50    109.00    5.40   1.80200 ! PROT alkane update, adm jr., 3/2/92

DIHEDRALS
OG2D1  CG2O4  CG321  CG2O4      0.0000  1     0.00 ! Properti , from OG2D1 CG2O4 CG311 NG2S1, PENALTY= 47
OG2D1  CG2O4  CG321  HGA2       0.0000  3   180.00 ! PALD, Propionaldehyde, PROT adm jr. 3/19/92, from lipid methyl acetate (unmodified because this may not be analogous to AALD)
HGR52  CG2O4  CG321  CG2O4      0.0000  3   180.00 ! Properti , from HGR52 CG2O4 CG311 NG2S1, PENALTY= 47
HGR52  CG2O4  CG321  HGA2       0.0000  3   180.00 ! acetaldehyde, adm 11/08

IMPROPERS
CG2O4  CG321  OG2D1  HGR52     50.0000  0     0.00 ! PALD from acetaldehyde adm 11/08

END
!RETURN

READ SEQUENCE CARDS 
* MA
*
   1   
MA

GENERATE MADH SETUP

!__ set conf no __
!set ii _NUM_
set ii 70
read coor pdb name xdd_@ii.pdb

define SOLUTE sele all end
print coor sele SOLUTE end
coor orient

cons fix sele type C2 end

!=========================================================
! solvate with water sphere
!=========================================================

set radius 13.0
set overlap 1.5
bomlev -2
    read sequence tip3 1000
    gene SOL noangle nodihe
    stream ../data/wat1000.str

    !-- set cutoff radius and cut --
    dele atom sele .byres. (segid SOL .and. type OH2 .and. .not. -
        (point 0.0 0.0 0.0 cut @radius) -
        ) end
    !-- delete overlapping atoms --
    dele atom sele .byres. (segid SOL .and. (SOLUTE .around. @overlap)) end

    set I 0
    set IMAX 20
    label XLOOP
        !-- duplicate waterbox, translate, rotate and repack --
        read sequence tip3 1000
        generate SOL1 noangle nodihe
        coor duplicate  sele segid SOL end sele segid SOL1 end
        coor rotate     xdir 1.0 phi 30.0 sele segid SOL1 end
        coor translate  xdir -1.0 sele segid SOL1 end

        !-- set cutoff radius and cut --
        dele atom sele .byres. (segid SOL1 .and. type OH2 .and. .not. -
            (point 0.0 0.0 0.0 cut @radius) -
            ) end
        !-- delete overlapping atoms --
        dele atom sele .byres. (segid SOL1 .and. ((segid SOL .or. SOLUTE) .around. @overlap)) end
        join SOL SOL1 renumber
        
        incr I by 1
        if I le @IMAX goto XLOOP

    set I 0
    set IMAX 20
    label YLOOP
        !-- duplicate waterbox, translate, rotate and repack --
        read sequence tip3 1000
        generate SOL1 noangle nodihe
        coor duplicate  sele segid SOL end sele segid SOL1 end
        coor rotate     ydir 1.0 phi 30.0 sele segid SOL1 end
        coor translate  ydir -1.0 sele segid SOL1 end

        !-- set cutoff radius and cut --
        dele atom sele .byres. (segid SOL1 .and. type OH2 .and. .not. -
            (point 0.0 0.0 0.0 cut @radius) -
            ) end
        !-- delete overlapping atoms --
        dele atom sele .byres. (segid SOL1 .and. ((segid SOL .or. SOLUTE) .around. @overlap)) end
        join SOL SOL1 renumber
        
        incr I by 1
        if I le @IMAX goto YLOOP

    !-- set smaller cutoff radius and cut --
    calc radius = @radius
    dele atom sele .byres. (segid SOL .and. type OH2 .and. .not. -
        (point 0.0 0.0 0.0 cut @radius) -
        ) end
bomlev 0

!=========================================================
! DFTB and NB settings : deactivated for fixed solute
!=========================================================

define qm sele segid MADH end
scalar WMAIN set 1.0 sele (qm) .and. type H*  SHOW end
scalar WMAIN set 2.0 sele (qm) .and. type O*  SHOW end
scalar WMAIN set 3.0 sele (qm) .and. type C*  SHOW end
SCCDFTB remove CHRG 0 SELE QM END TEMP 0.0 SCFT 0.00000001 D3RD HBOND TWOBody THREebody

shake bonh sele (all .and. .not. type H3) end

NBONDS  GROUP  SWITCH CDIE  VDW VSWI  EXTEND GRAD QUAD -
        CUTNB 13.0  CTOFNB 12.0 CTONNB 8.0  WMIN 1.5  EPS 1.0 -
        inbfrq -1


MMFP
GEO  sphere quartic -
    force 400.0 droff 12.0 p1 2.25 select type OH2 end
END

energy

!=========================================================
! use umbrella restraints : deactivated for fiexd molecule
!=========================================================

cons dihe bynum 5 1 2 3 force 1000.0 min 0.0 width 0.0 period 0
cons dihe bynum 1 2 3 4 force 1000.0 min 0.0 width 0.0 period 0

define o1   sele bynu 4 show end
define h3   sele bynu 8 show end
define o2   sele bynu 5 show end

rxncor: define xo1  point sele o1 show end
rxncor: define xh3  point sele h3 show end
rxncor: define xo2  point sele o2 show end

rxncor: define d1   dist xo1 xh3
rxncor: define d2   dist xo2 xh3
rxncor: define dd   combination d1 1.0 d2 -1.0
rxncor: set nrxn 1 dd

!__ match cv to conf no. __
calc xdd = -3.50 + ( (@ii - 1) * 0.05)

rxncor: umbrella name dd kumb 0.0 del0 @xdd form 1
rxncor: statistics name dd lowdelta 0.0 hidelta 0.0 deldel 0.0 start 0

SCALAR FBETA SET 5.0 sele all end
!============ for test =============
dynamics leap LANGEVIN start nstep 50000 timestep 0.0001 -
    ILBFRQ 1000 RBUFFER 0.0 TBATH 300.0 -
    FIRSTT 300.0 FINALT 300.0  -
    iprfrq 1000 -
    INBFRQ 10 -
    IUNXYZ -1 IUNCRD -1 KUNIT -1

write coor card name temp.cor

bomlev -2
    read sequence tip3 1000
    gene SOL1 noangle nodihe
    read coor card name ../data/wat1000.cor append

    !-- set cutoff radius and cut --
    dele atom sele .byres. (segid SOL1 .and. type OH2 .and. .not. -
        (point 0.0 0.0 0.0 cut @radius) -
        ) end
    !-- delete overlapping atoms --
    dele atom sele .byres. (segid SOL1 .and. ((segid SOL .or. SOLUTE) .around. @overlap)) end
    join SOL SOL1 renumber
bomlev 0

!============ shake ================
shake bonh sele (all .and. .not. type H3) end

MMFP
GEO  sphere quartic -
    force 400.0 droff 12.0 p1 2.25 select type OH2 end
END

write psf card name frames_@ii.psf
write coor card name frames_@ii.cor

open write unit 18 form name frames_@ii.xyz
open write unit 17 file name frames_@ii.dcd 
open write card unit 19 name en_@ii.dat

SCALAR FBETA SET 5.0 sele all end
!============ for test =============
dynamics leap LANGEVIN start nstep 1000000 timestep 0.0001 -
    ILBFRQ 1000 RBUFFER 0.0 TBATH 300.0 -
    FIRSTT 300.0 FINALT 300.0  -
    iprfrq 2000 -
    INBFRQ 10 -
    mxyz 4 IUNXYZ 18 NSAVX 2000 - !write xyz 50000
    IUNCRD 17 NSAVC 2000 -
    KUNIT 19 NPRINT 2000 !write energy file 50000

stop

