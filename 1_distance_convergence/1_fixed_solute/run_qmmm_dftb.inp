*malon md in sol
*tanmoy
*
!++++ DO NOT CHANGE TITLE ++++ SED IS HARD CODED ++++

!--load topology for small molecules--
set t /projectnb/cui-buchem/tanmoy/projects/toppar

read rtf card name @t/top_all36_cgenff.rtf
read para card flex name @t/par_all36_cgenff.prm
stream @t/toppar_water_ions.str

set ii _PART_
read psf card name          fixed_solute_md/frames_@ii.psf
!read coor card name        fixed_solute_md/frames_@ii.cor
open read unit 22 file name fixed_solute_md/frames_@ii.dcd

trajectory firstu 22 nunit 1 skip 0

set jj 0
set jmax 500

open write card unit 23 name en_dftb_@ii.dat
SYSTEM "rm forces_dftb.dat forces_ma_dftb.dat"  !frames.cor

label loop
    read psf card name fixed_solute_md/frames_@ii.psf
    traj read
    
        !<<<<<<<< WARNING >>>>>>>>
        ! the if clause is necessary because charmm makes the coordinates of the fixed atom 99999 from 2nd frame

        if jj eq 0 then
            define fxd sele segid MADH end
            write coor card name solute_frm_dftb.cor sele fxd end
        else
            coor copy comp sele (all .and. .not. fxd) end
            read coor card name solute_frm_dftb.cor
            coor add comp
        endif
        
        !write coor pdb name temp.pdb sele segid MADH end
        !SYSTEM "cat temp.pdb >> frames.pdb"

        calc radius = ( _RNUM_ * 0.2)
        !-- set cutoff radius and cut --
        dele atom sele .byres. (segid SOL .and. type OH2 .and. .not. -
            (point 0.0 0.0 0.0 cut @radius) -
            ) end

        !=========================================================
        ! DFTB and NB settings 
        !=========================================================

        define qm sele segid MADH end
        scalar WMAIN set 1.0 sele (qm) .and. type H*  SHOW end
        scalar WMAIN set 2.0 sele (qm) .and. type O*  SHOW end
        scalar WMAIN set 3.0 sele (qm) .and. type C*  SHOW end
        SCCDFTB remove CHRG 0 SELE QM END TEMP 0.0 SCFT 0.00000001 D3RD HBOND TWOBody THREebody

        energy
        write title unit 23
        * ?ENER

        coor force comp
        write coor comp card name temp_dftb.dat
        
        SYSTEM "awk '{print $5, $6, $7}' temp_dftb.dat | sed 1,3d >> forces_dftb.dat"
        SYSTEM "awk '{print $5, $6, $7}' temp_dftb.dat | sed 1,3d | head -n 10 >> forces_ma_dftb.dat"

        dele atoms sele all end
    incr jj by 1
    if jj lt @jmax goto loop
stop 
